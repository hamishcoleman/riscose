AC_INIT(src/monty/monty.c)
AC_CONFIG_AUX_DIR(util)
AM_INIT_AUTOMAKE(riscose,0.01)
AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AC_PROG_YACC
AC_PROG_RANLIB
AC_PROG_AWK
AC_PATH_PROG(BINAWK, $AWK)
test -z "$BINAWK" && AC_MSG_ERROR([no acceptable $AWK found in \$PATH])
AC_PATH_PROG(PERL, perl)
test -z "$PERL" && AC_MSG_ERROR([no acceptable $ac_word found in \$PATH])

AC_DEFINE(CONF_DEBUGGING, 1, [enable -D command line debug option.])

ARM=armul
AC_ARG_ENABLE(arm,
[  --enable-arm=ARM        Set the ARM emulation to use.  Choose one of
                          armul, sleeve, or native [armul]],
    ARM=$enableval)
case "$ARM" in
armul|sleeve) ;;
*) AC_MSG_ERROR([only ARM=armul or ARM=sleeve handled at the moment])
esac
AC_SUBST(ARM)
AM_CONDITIONAL(ARM_ARMUL, test x$ARM = xarmul)
AM_CONDITIONAL(ARM_SLEEVE, test x$ARM = xsleeve)

# FIXME: we should only use readline if --with-readline is used, or at
# least allow it to be disabled with --without-readline.
AC_CHECK_HEADERS(readline.h readline/readline.h)

AC_C_INLINE

AC_OUTPUT([ \
        Makefile \
        man/Makefile \
        src/Makefile \
        src/armul/Makefile \
        src/defmod/Makefile \
        src/defmod/bootstrap/Makefile \
        src/include/Makefile \
        src/monty/Makefile \
        src/native/Makefile \
        src/os/Makefile \
        src/oslib/Makefile \
        src/osapi/Makefile \
        src/rom/Makefile \
        src/rom/compilerom \
        src/sleeve/Makefile \
        tests/Makefile \
        tests/armas \
        util/Makefile \
    ], \
    [chmod +x src/rom/compilerom tests/armas]
    )
